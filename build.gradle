import java.nio.file.Paths

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'org.springframework.boot' version '3.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

group 'edu.utica'
version = '1.0'

def publishProps = {
    Properties properties = new Properties()
    Paths.get(System.properties.'user.home','.credentials','githubPackagesPublish.properties').withInputStream {
        properties.load(it)
    }
    return properties
}.call()

publishing {
    repositories {
        maven {
            name = 'jobsub-mail-starter'
            url = uri('https://maven.pkg.github.com/utica-iits-ais/jobsub-starter')
            credentials {
                username = publishProps.username
                password = publishProps.password
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    api project(':jobsub-hello-starter')
}
